cmake_minimum_required(VERSION 3.19)
cmake_policy(SET CMP0074 NEW)

project(OCRA LANGUAGES CXX)

#[[Rendering API]]
set(OCRA_API_IMPL "OpenGL" CACHE STRING
  "API Impl used at compile-time")
set(OCRA_API_IMPL_VALUES
  "OpenGL;Vulkan;DirectX")
set_property(CACHE OCRA_API_IMPL PROPERTY STRINGS ${OCRA_API_IMPL_VALUES})

add_compile_definitions(
  OCRA_API_OpenGL=0
  OCRA_API_Vulkan=1
  OCRA_API_DirectX=2
  OCRA_API_IMPL=OCRA_API_${OCRA_API_IMPL})

set(OCRA_API_HEADER_LIST
  OCRA/Allocator.hpp
  OCRA/Commands.hpp
  OCRA/Enums.hpp
  OCRA/Extent.hpp
  OCRA/Flags.hpp
  OCRA/Handle.hpp
  OCRA/Handles.hpp
  OCRA/Structs.hpp
  OCRA/Vec.hpp
  OCRA/Vec_Boolean.hpp
  OCRA/Vec_Functor.hpp
  OCRA/Vec_Interpolation.hpp
  OCRA/Vec_Math.hpp
  OCRA/Common/IndexType.hpp
  OCRA/Common/Timer.hpp
  OCRA/Methods/Buffer.hpp
  OCRA/Methods/CommandBuffer.hpp
  OCRA/Methods/CommandPool.hpp
  OCRA/Methods/DescriptorPool.hpp
  OCRA/Methods/DescriptorSet.hpp
  OCRA/Methods/Device.hpp
  OCRA/Methods/Fence.hpp
  OCRA/Methods/Image.hpp
  OCRA/Methods/Instance.hpp
  OCRA/Methods/Memory.hpp
  OCRA/Methods/PhysicalDevice.hpp
  OCRA/Methods/Queue.hpp
  OCRA/Methods/QueryPool.hpp
  OCRA/Methods/Semaphore.hpp
)

set(INCLUDE_DIR ${CMAKE_CURRENT_BINARY_DIR}/include)
set(HEADER_PATH ${CMAKE_CURRENT_BINARY_DIR}/include/OCRA/OCRA.hpp)

file(WRITE ${HEADER_PATH}
"#pragma once
/**
* This file contains every includes
*/

")
foreach(File ${OCRA_API_HEADER_LIST})
  file(APPEND ${HEADER_PATH}
  "#include<${File}>\n")
endforeach()

list(TRANSFORM OCRA_API_HEADER_LIST PREPEND "${CMAKE_CURRENT_SOURCE_DIR}/include/" OUTPUT_VARIABLE OCRA_API_HEADER_FILES)

add_library(OCRA-API-Interface INTERFACE ${OCRA_API_HEADER_FILES} ${HEADER_PATH})
add_library(OCRA::API::Interface ALIAS OCRA-API-Interface)
set_target_properties(OCRA-API-Interface PROPERTIES LINKER_LANGUAGE CXX)
target_include_directories(OCRA-API-Interface INTERFACE
  "${CMAKE_CURRENT_SOURCE_DIR}/include"
  "${INCLUDE_DIR}"
)

set_target_properties(
  OCRA-API-Interface
  PROPERTIES FOLDER "API"
)

if (OCRA_API_IMPL STREQUAL "OpenGL")
  add_subdirectory(Driver/OpenGL)
elseif (OCRA_API_IMPL STREQUAL "Vulkan")
  add_subdirectory(Driver/Vulkan)
endif (OCRA_API_IMPL STREQUAL "OpenGL")

if(MSVC_IDE)
  set(CMAKE_DEBUG_POSTFIX "d")
  # Run macro
  GroupSources(src)
  GroupSources(include)
endif()
